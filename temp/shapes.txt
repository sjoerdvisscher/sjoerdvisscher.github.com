do(

; define the Shape base
Shape=(
	x=int()
	y=int()
	moveTo?=do(
		x.set(newx)
		y.set(newy)
	)
	rMoveTo?=do(
		x.set(x.add(deltax))
		y.set(y.add(deltay))
	)
	draw?=expr()
)

; define the Rectangle base
Rectangle=Shape(
	width=int()
	height=int()
	draw?=document.write(
		("Drawing a Rectangle at:(" x "," y "), width " width ",height " height).concat()
	)	
)

; define the Circle base
Circle=Shape(
	radius=int()
	draw?=document.write(
		("Drawing a Circle at:(" x "," y "), raduis " radius).concat()
	)	
)

; create a list containing various shape instances
scribble=(
	Shape()
)(
	Rectangle(x=10 y=20 width=5 height=6)
	Circle(x=15 y=25 radius=8)
)

; create an rectangle instance
aRec=Rectangle(x=0 y=0 width=15 height=15)

; handle the shapes polymorphically
scribble.each(
	do(
		data.draw()
		data.rMoveTo(deltax=100 deltay=100)
		data.draw()
	)
)

; set the width of the rectangle inctance
aRec.width.set(30)
aRec.draw()

)